#-*-shell-script-*-
# vim: ft=zsh
bindkey -e               # emacs key bindings
source ~/.dotfiles/zsh/plugins.zsh

export RSYNC_RSH='ssh'
export EDITOR=nvim
export VISUAL=nvim
export PAGER='less'
export LESS='--quit-if-one-screen -R -iMX'
export LESSOPEN=|~/.dotfiles/lessfilter %s
export WORDCHARS=${WORDCHARS:s|/||}
export PATH=~/bin/:~/.local/bin:$PATH

HISTSIZE=5100
export HISTFILE=~/.zsh_history
SAVEHIST=5000
DIRSTACKSIZE=15
fignore=('~' .bak)
hash -d 'D=/usr/share/doc'

alias ls='exa --icons'
alias l='exa --icons -l'
alias la='exa --icons -la'
alias pstree='pstree -A'
alias mv='nocorrect mv'       # no spelling correction on mv
alias cp='nocorrect cp'       # no spelling correction on cp
# alias ls='ls --color=auto -F'
# alias ll='ls --color=auto -Fl'
# alias la='ls --color=auto -Fla'
# alias dir='ls -l -h'
alias rm='rm -i'
alias gf='git flow'
alias dco='docker-compose'
alias kc='kubectl'
RPROMPT=`print -n '%{\033[1;33m%}(%T)%b%{\033[0m%}'`
source ~/.dotfiles/zsh/alias.zsh

setopt   notify globdots correct cdablevars auto_menu auto_list
setopt   correctall noautocd recexact longlistjobs sharehistory
setopt   autoresume extendedglob rcquotes hashcmds
setopt   globcomplete ignore_eof rmstarsilent histallowclobber
setopt   auto_pushd pushdminus print_eight_bit
setopt   magic_equal_subst transient_rprompt
unsetopt bgnice listbeep recexact listambiguous 

bindkey ' ' magic-space  # also do history expansino on space
bindkey "\e[Z" reverse-menu-complete

autoload -Uz up-line-or-beginning-search
autoload -Uz down-line-or-beginning-search
zle -N up-line-or-beginning-search
zle -N down-line-or-beginning-search
bindkey "^P" up-line-or-beginning-search
bindkey "^N" down-line-or-beginning-search

autoload -Uz edit-command-line
zle -N edit-command-line
bindkey '\ee' edit-command-line
precmd() {
	[ -n "$TMUX_PANE" ] && tmux rename-window $(basename $PWD)	
	jobs;
}

# The following lines were added by compinstall
autoload -Uz compinit && compinit -u

autoload -Uz vcs_info

bindkey '^[;' autosuggest-execute
# accept auto completion
function accept_and_pager() {
	BUFFER="${BUFFER} | ${PAGER}"
	zle accept-line
}
zle -N accept_and_pager
bindkey '^[p' accept_and_pager
function auto_quote() {
	if [ "$#LBUFFER" -eq 0 ]; then
		zle quote-line
	elif [ "${BUFFER:$#BUFFER-1}" = " " ]; then
		RBUFFER="\"\""
		zle forward-char
	else
		zle backward-word
		RBUFFER="\"${RBUFFER}\""
		zle end-of-line
	fi
}
zle -N auto_quote
bindkey '^[7' auto_quote
# fzf completion
function auto_fzr() {
  TARGET=$(fd | fzf)
  BUFFER="${BUFFER} ${TARGET}"
}
zle -N auto_fzr
bindkey '^[i' auto_fzr

bindkey '^[k' history-substring-search-up
bindkey '^[j' history-substring-search-down

zstyle ':completion:*' use-cache true
zstyle ':completion:*' auto-description 'specify: %d'
zstyle ':completion:*' file-sort name
zstyle ':completion:*' format 'Completing %d'
zstyle ':completion:*' ignore-parents parent
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' max-errors 4
zstyle ':completion:*' menu select=0
zstyle ':completion:*' prompt 'correct'
zstyle ':completion:*' squeeze-slashes true
zstyle ':completion:*' substitute 1
zstyle ':completion:*' verbose true
zstyle :compinstall filename '/home/tshm/.zshrc'
# End of lines added by compinstall

NIX=~/.nix-profile/etc/profile.d/nix.sh
[ -e $NIX ] && source $NIX

if [ -f ~/.zshrc.local ]; then
    source ~/.zshrc.local
fi
